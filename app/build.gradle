apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.neoranga55.cleanguitestarchitecture"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.neoranga55.cleanguitestarchitecture.cucumber.runner.CucumberTestRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            buildConfigField "String", "TEST_TAGS", "\"${getTestTags()}\""
        }
    }
}

// Task to recover Cucumber test reports
gradle.taskGraph.afterTask { task, TaskState state ->
    if (task == connectedAndroidTest) {
        def reportDir = new File(project.buildDir, "cucumber-reports")
        if (!reportDir.exists()) {
            reportDir.mkdirs()
        }
        println 'Retrieving Cucumber test report to ' + project.buildDir
        def processBuilder = new ProcessBuilder(
                ['adb', 'pull', '/mnt/sdcard/cucumber-reports/', reportDir.getAbsolutePath()])
        processBuilder.start()
    }
}

// Read Cucumber tags from command line like: ./gradlew connectedAndroidTest -Dtags="@login-scenarios,@kitkat"
def getTestTags() {
    return project.hasProperty("tags") ? project.getProperties().get("tags") : ""
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.0.1'
    compile 'com.google.android.gms:play-services:7.8.0'
    // Espresso
    androidTestCompile 'com.android.support.test:runner:0.3'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2'
    androidTestCompile 'com.android.support:support-annotations:22.0.1'
    // Cucumber
    androidTestCompile 'info.cukes:cucumber-android:1.2.2'
    androidTestCompile 'info.cukes:cucumber-picocontainer:1.2.2'
    // Spoon for screenshots
    androidTestCompile 'com.squareup.spoon:spoon-client:1.2.0'
}
